
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ДокМета = Метаданные.Документы;
	Запрос = Новый Запрос;
	
	Для Каждого Эл Из Метаданные.ПланыОбмена.ОС_ОбменСМобильнымиУстройствами.Состав Цикл
		ЭлМета = Эл.Метаданные;
		Если Эл.АвтоРегистрация = АвтоРегистрацияИзменений.Разрешить И ДокМета.Содержит(ЭлМета) Тогда
			Запрос.Текст = Запрос.Текст + ?(ПустаяСтрока(Запрос.Текст),"", Символы.ПС +
			" ОБЪЕДИНИТЬ ВСЕ " 
			+ Символы.ПС) 
			+ "ВЫБРАТЬ 
			|	Ссылка Как Документ,
			|	Ссылка.Дата Как Дата,
			|	""" + ЭлМета.ПолноеИмя() + """ Как ТипДокумента,
			|	1 Как КоличествоДокументов Из " + ЭлМета.ПолноеИмя() + ".Изменения";  	
		КонецЕсли;	
	КонецЦикла;	
	
	Запрос.Текст = "Выбрать ТипДокумента, Документ, КоличествоДокументов Из ( " + Запрос.Текст + " ) КАК ТЧ 
	|	УПОРЯДОЧИТЬ ПО ТипДокумента, ТЧ.Дата
	|	ИТОГИ СУММА(КоличествоДокументов) 
	|	ПО ТипДокумента ";
	
	мДерево = РеквизитФормыВЗначение("Изменения");
	мДерево.Строки.Очистить();
	
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока Выборка.Следующий() Цикл
		НоваяСтрока                      = мДерево.Строки.Добавить();
		НоваяСтрока.Документ             = Выборка.ТипДокумента;
		НоваяСтрока.КоличествоДокументов = Выборка.КоличествоДокументов;
		ВыборкаДеталь = Выборка.Выбрать();
		Пока ВыборкаДеталь.Следующий() Цикл
			НоваяСтрокаПодч = НоваяСтрока.Строки.Добавить();
			НоваяСтрокаПодч.Документ             = ВыборкаДеталь.Документ;
			НоваяСтрокаПодч.КоличествоДокументов = ВыборкаДеталь.КоличествоДокументов; 			
		КонецЦикла;
	КонецЦикла;	
	
	ЗначениеВРеквизитФормы(мДерево,"Изменения");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Для Каждого Эл Из Изменения.ПолучитьЭлементы() Цикл
		Элементы.Изменения.Развернуть(Эл.ПолучитьИдентификатор(), Истина);
	КонецЦикла;
КонецПроцедуры
