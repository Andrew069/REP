
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Контрагент = Параметры.Контрагент;
	СформироватьОтчет();
КонецПроцедуры

&НаСервере
Процедура СформироватьОтчет ()
	Запрос       = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеПродаж.Номенклатура КАК Номенклатура,
	|	ДанныеПродаж.Дата КАК Дата,
	|	ДанныеПродаж.ПродажиСумма КАК Продажи,
	|	ДанныеПродаж.ВозвратыСумма КАК Возвраты,
	|	ДанныеПродаж.ВозвратыКоличество,
	|	ДанныеПродаж.ПродажиКоличество
	|ИЗ
	|	РегистрСведений.ОС_ДанныеПродажДляМП КАК ДанныеПродаж
	|ГДЕ
	|	ДанныеПродаж.Контрагент = &Контрагент
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата УБЫВ,
	|	Номенклатура
	|ИТОГИ ПО
	|	Дата";
	Запрос.УстановитьПараметр("Контрагент" ,Контрагент);
	
	ИтогПродажи  = 0;
	ИтогВозвраты = 0;
	
	Результат.Очистить();
	
	СЧ = 5;
	ВыборкаДата = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаДата.Следующий() Цикл
		СЧ = СЧ - 1;	
		Если СЧ = 0 Тогда
			Прервать;
		КонецЕсли;	
		ВыборкаНоменклатура = ВыборкаДата.Выбрать();
		Пока ВыборкаНоменклатура.Следующий() Цикл
			СтруктураПоиска = Новый Структура("Номенклатура",ВыборкаНоменклатура.Номенклатура);
			ИскомыеСтроки   = Результат.НайтиСтроки(СтруктураПоиска);
			Если ИскомыеСтроки.Количество() > 0 Тогда
				ИскомаяСтрока = ИскомыеСтроки[0];
			Иначе
				ИскомаяСтрока = Результат.Добавить();
				ЗаполнитьЗначенияСвойств(ИскомаяСтрока,СтруктураПоиска);
			КонецЕсли;
			ИскомаяСтрока["ПродажиВозвраты" + СЧ] = Формат(ВыборкаНоменклатура.ПродажиКоличество,"ЧДЦ=; ЧН=0; ЧГ=0") + "/" + Формат(ВыборкаНоменклатура.ВозвратыКоличество,"ЧДЦ=; ЧН=0; ЧГ=0");
			ИтогПродажи  = ИтогПродажи  + ВыборкаНоменклатура.Продажи; 
			ИтогВозвраты = ИтогВозвраты + ВыборкаНоменклатура.Возвраты; 
		КонецЦикла;	
	КонецЦикла;	
	
	Результат.Сортировать("Номенклатура");
	
	Для Каждого СтрокаТЧ Из Результат Цикл
		Для СЧ = 1 По 4 Цикл
			Если СтрокаТЧ["ПродажиВозвраты" + СЧ] = "" Тогда
				СтрокаТЧ["ПродажиВозвраты" + СЧ] = "0/0";	
			КонецЕсли;	
		КонецЦикла;	
	КонецЦикла;
	
	Если Результат.Количество() = 0 Тогда
		Элементы.ДекорацияНетДанных.Заголовок = "<< НЕТ ДАННЫХ >>";
	Иначе
		Элементы.ДекорацияНетДанных.Заголовок = "Итого: " + ИтогПродажи + " - " + (-ИтогВозвраты) + " = " + (ИтогПродажи + ИтогВозвраты) + " р.";
	КонецЕсли;	
КонецПроцедуры	