
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбновитьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КомандаОбновить(Команда)
	ОбновитьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОбновитьНаСервере ()
	ДатаПоследнейОчисткиЛогов = Константы.ДатаПоследнейОчисткиЛогов.Получить();
	
	Логи.Очистить();
	
	Чтение = Новый ЧтениеТекста;
	Чтение.Открыть(РаботаСЖурналом.ПолучитьИмяФайлаЛогов(),"UTF-8");
	
	Строка = Чтение.ПрочитатьСтроку();
	СЧ = 0;
	Пока Строка <> Неопределено Цикл
		СЧ = СЧ + 1;
		НоваяСтрока        = Логи.Добавить();
		НоваяСтрока.Строка = Строка;
		НоваяСтрока.СЧ     = СЧ;
		Строка             = Чтение.ПрочитатьСтроку();
	КонецЦикла;	
	
	Логи.Сортировать("СЧ Убыв");
	
	Чтение.Закрыть();
КонецПроцедуры	

&НаКлиенте
Процедура КомандаОчистить(Команда)
	РаботаСЖурналом.ОчиститьФайлЛогов();
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтправитьНаПочту(Команда)
	#ЕСЛИ МобильноеПриложениеКлиент Тогда
		ЗапускМП          = Новый ЗапускПриложенияМобильногоУстройства();
		ЗапускМП.Действие = "android.intent.action.SEND";
		ЗапускМП.Данные   = "1c@balmiko.ru";
		ЗапускМП.Тип      = "text/plain";;
		
        ЗапускМП.ДополнительныеДанные.Добавить("android.intent.extra.SUBJECT", "ЛОГ УСТРОЙСТВА"); // тема сообщения
		
		СтрКонфигурации = ПолучитьИнфуОКонфигурацииСервер();
		СтрокаОшибки = "";
		СтрокаОшибки = СтрокаОшибки + Символы.ПС;
		Для Каждого Стр Из СтрКонфигурации Цикл 
			СтрокаОшибки = СтрокаОшибки + Стр.Ключ + ": " + Стр.Значение + Символы.ПС;
		КонецЦикла;	     
		ЗапускМП.ДополнительныеДанные.Добавить("android.intent.extra.TEXT",СтрокаОшибки);
		
		Чтение = Новый ЧтениеТекста;
		Чтение.Открыть(РаботаСЖурналом.ПолучитьИмяФайлаЛогов(),"UTF-8");
		Текст = Чтение.Прочитать();
		
		ИмяФайлаЗаписи = "/sdcard/Download/Data.txt";
		Запись         = Новый ЗаписьТекста;
		Запись.Открыть(ИмяФайлаЗаписи,"UTF-8");
		Запись.Записать(Текст);
		Запись.Закрыть();
		
		ЗапускМП.ДополнительныеДанные.Добавить("android.intent.extra.STREAM","file://" + ИмяФайлаЗаписи,"Uri");
		
		ЗапускМП.Запустить(ИСТИНА);
	#КОНЕЦЕСЛИ		
КонецПроцедуры

&НаСервере
Функция ПолучитьИнфуОКонфигурацииСервер ()
	Возврат ПолучитьИнфуОКонфигурации();
КонецФункции	

&НаКлиенте
Процедура ЛогиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = ЛОЖЬ;
	ТекСтрока = Элементы.Логи.ТекущиеДанные;
	ОткрытьФорму("Обработка.ПросмотрЛогов.Форма.ФормаПросмотра",Новый Структура("ТекстОшибки",ТекСтрока.Строка));	
КонецПроцедуры
