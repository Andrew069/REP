
Процедура ОтметитьВыполнениеЗаданияТП(Маршрут, Контрагент, ВидЗадания, ДокументСсылка, ШаблонАнкеты = Неопределено) Экспорт
	Перем Запрос, РезультатЗапроса, ВыборкаДетальныеЗаписи, МенеджерЗаписи;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаданияТП.Маршрут,
	|	ЗаданияТП.ВидЗадания,
	|	ЗаданияТП.Контрагент КАК Контрагент,
	|	ИСТИНА КАК Выполнено,
	|	ЗНАЧЕНИЕ(Документ.Посещение.ПустаяСсылка) КАК Посещение,
	|	NULL КАК ШаблонАнкеты
	|ИЗ
	|	РегистрСведений.ЗаданияТП КАК ЗаданияТП
	|ГДЕ
	|	ЗаданияТП.Маршрут = &Маршрут
	|	И ЗаданияТП.ВидЗадания = &ВидЗадания
	|	И ЗаданияТП.Контрагент = &Контрагент
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаданияТППоВнеплановымПосещениям.Маршрут,
	|	ЗаданияТППоВнеплановымПосещениям.ВидЗадания,
	|	ЗаданияТППоВнеплановымПосещениям.Контрагент,
	|	ИСТИНА,
	|	ЗаданияТППоВнеплановымПосещениям.Посещение,
	|	ЗаданияТППоВнеплановымПосещениям.ШаблонАнкеты
	|ИЗ
	|	РегистрСведений.ЗаданияТППоВнеплановымПосещениям КАК ЗаданияТППоВнеплановымПосещениям
	|ГДЕ
	|	ЗаданияТППоВнеплановымПосещениям.Маршрут = &Маршрут
	|	И ЗаданияТППоВнеплановымПосещениям.ВидЗадания = &ВидЗадания
	|	И ЗаданияТППоВнеплановымПосещениям.Контрагент = &Контрагент";
	Если ДокументСсылка.Пустая() Тогда
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.Посещение") Тогда
		Запрос.Текст = Запрос.Текст + "
		|	И ЗаданияТППоВнеплановымПосещениям.Посещение = &Посещение";
		Запрос.УстановитьПараметр( "Посещение", ДокументСсылка );
	Иначе
		Запрос.Текст = Запрос.Текст + "
		|	И ЗаданияТППоВнеплановымПосещениям.Посещение = &Посещение";
		Запрос.УстановитьПараметр( "Посещение", ДокументСсылка.Посещение );
	КонецЕсли;
	Если ТипЗнч(ВидЗадания) = Тип("Строка") Тогда
		Запрос.УстановитьПараметр( "ВидЗадания", ОбщийМодульПовтИсп.ВидыЗаданийТорговымПредставителямПолучитьПредопределенныйЭлемент(ВидЗадания) );
	Иначе
		Запрос.УстановитьПараметр( "ВидЗадания", ВидЗадания );
	КонецЕсли;
	
	//Киселев 19-09-2017
	Если ШаблонАнкеты <> Неопределено Тогда
		Запрос.Текст = Запрос.Текст + "
		|	И ЗаданияТППоВнеплановымПосещениям.ШаблонАнкеты = &ШаблонАнкеты";
		Запрос.УстановитьПараметр("ШаблонАнкеты", ШаблонАнкеты);
	КонецЕсли;
		
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("Маршрут", Маршрут);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		МенеджерЗаписи = РегистрыСведений.ВыполнениеЗаданийТП.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Маршрут = Маршрут;
		МенеджерЗаписи.ВидЗадания = ВыборкаДетальныеЗаписи.ВидЗадания;
		МенеджерЗаписи.Контрагент = Контрагент;
		МенеджерЗаписи.ШаблонАнкеты = ВыборкаДетальныеЗаписи.ШаблонАнкеты;
		Если ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.Посещение") Тогда
			МенеджерЗаписи.Посещение = ДокументСсылка;
		Иначе
			МенеджерЗаписи.Посещение = ДокументСсылка.Посещение;
		КонецЕсли;
		МенеджерЗаписи.Выполнено = Истина;
		МенеджерЗаписи.Документ = ДокументСсылка;
		МенеджерЗаписи.Записать( Истина );
	КонецЦикла;
	
КонецПроцедуры

Процедура УдалитьВыполнениеЗаданияТП( ДокументСсылка ) Экспорт
	Перем Запрос, РезультатЗапроса, ВыборкаДетальныеЗаписи, МенеджерЗаписи;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВыполнениеЗаданийТП.Маршрут,
	|	ВыполнениеЗаданийТП.ВидЗадания,
	|	ВыполнениеЗаданийТП.Контрагент,
	|	ВыполнениеЗаданийТП.Посещение
	|ИЗ
	|	РегистрСведений.ВыполнениеЗаданийТП КАК ВыполнениеЗаданийТП
	|ГДЕ
	|	ВыполнениеЗаданийТП.Документ = &Документ";
	Если Не ДокументСсылка.Пустая() Тогда
		Запрос.Текст = Запрос.Текст + "
		|	И ВыполнениеЗаданийТП.Посещение = &Посещение";
		Запрос.УстановитьПараметр( "Посещение", ДокументСсылка.Посещение );
	КонецЕсли;
	Запрос.УстановитьПараметр("Документ", ДокументСсылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		МенеджерЗаписи = РегистрыСведений.ВыполнениеЗаданийТП.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Маршрут = ВыборкаДетальныеЗаписи.Маршрут;
		МенеджерЗаписи.ВидЗадания = ВыборкаДетальныеЗаписи.ВидЗадания;
		МенеджерЗаписи.Контрагент = ВыборкаДетальныеЗаписи.Контрагент;
		МенеджерЗаписи.Посещение = ВыборкаДетальныеЗаписи.Посещение;
		МенеджерЗаписи.Удалить();
	КонецЦикла;
	
КонецПроцедуры
