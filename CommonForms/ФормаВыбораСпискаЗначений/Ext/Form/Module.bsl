
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Вопрос = Параметры.Вопрос;
	Заголовок = Вопрос.Наименование;
	Если Не ПустаяСтрока(Вопрос.Подсказка) Тогда
		Элементы.ДекорацияПодсказка.Заголовок = Вопрос.Подсказка;
	Иначе
		Элементы.ДекорацияПодсказка.Видимость = Ложь;
	КонецЕсли;
	ТЗ = ПолучитьИзВременногоХранилища(Параметры.АдресТаблицы);
	Таблица.Загрузить(Вопрос.ВариантыОтветов.Выгрузить());
	Для Каждого СтрокаТЧ Из ТЗ Цикл
		ИскомыеСтроки = Таблица.НайтиСтроки(Новый Структура("ВариантОтвета",СтрокаТЧ.Ответ));
		Если ИскомыеСтроки.Количество() > 0 Тогда
			ИскомаяСтрока = ИскомыеСтроки[0];
			ИскомаяСтрока.Выбран = Истина;
			Если ИскомаяСтрока.УказатьПояснение Тогда
				ИскомаяСтрока.Пояснение = СтрокаТЧ.Пояснение;
			КонецЕсли;	
		КонецЕсли;	
	КонецЦикла;	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОК(Команда)
	Адрес = ПодготовитьАдресТаблицы();
	Закрыть(Адрес);
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	Закрыть();
КонецПроцедуры

&НаСервере
Функция ПодготовитьАдресТаблицы ()
	ТЗ = Таблица.Выгрузить(Новый Структура("Выбран",Истина),"ВариантОтвета,Пояснение");
	Возврат ПоместитьВоВременноеХранилище(ТЗ,Новый УникальныйИдентификатор);
КонецФункции	