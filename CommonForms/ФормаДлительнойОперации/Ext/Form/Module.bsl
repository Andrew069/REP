
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ВернутьРезультат    = Параметры.ВернутьРезультат;
	Заголовок           = Параметры.ЗаголовокФормы;
	ИмяПроцедуры        = Параметры.ИмяПроцедуры;
	ДопПараметрыЗадания = Параметры.ДопПараметры;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПодключитьОбработчикОжидания("ОбработчикОбновленияВремени",1,Истина);
	ОбработчикНачалаСинхронизации(ДопПараметрыЗадания);
КонецПроцедуры

&НаКлиенте
Процедура КомандаПрервать(Команда)
	Рез = ФоновыеЗаданияСервер.ОтменитьВыполнениеЗадания(ИДСинхронизации);
	ИДСинхронизации = Неопределено;
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОбновленияВремени () Экспорт
	Элементы.ДатаВремя.ПодсказкаВвода = ОбщийМодульСервер.ФорматДаты(ТекущаяДата());
	Элементы.ДатаВремя.ОбновитьТекстРедактирования();
	ПодключитьОбработчикОжидания("ОбработчикОбновленияВремени",1,Истина);
КонецПроцедуры	

&НаКлиенте
Процедура ОбработчикНачалаСинхронизации(ДопПараметры = Неопределено)
	ФоновыеЗаданияКлиент.ОбработчикНачалаСинхронизации(ЭтаФорма,ИмяПроцедуры,ДопПараметры); 
КонецПроцедуры	

&НаКлиенте
Процедура ОбработчикОбновленияСостоянияСинхронизации ()
	ФоновыеЗаданияКлиент.ОбработчикОбновленияСостоянияСинхронизации(ЭтаФорма);
КонецПроцедуры	

&НаКлиенте
Процедура ДекорацияСостояниеОбменаНажатие(Элемент)
	ОткрытьФорму("Обработка.ОбработкаОшибокНаКлиенте.Форма.Форма",Новый Структура("ТекстОшибки",ТекстОшибки));
КонецПроцедуры

&НаКлиенте
Процедура ОбновлениеПослеСинхронизации () Экспорт 
	Если ИмяПроцедурыСинхронизации = "ОбщийМодульСервер.ОбновитьКонфигурацию" Тогда
		ОбновитьКонфигурациюЗавершение();
	КонецЕсли;
	
	Если ВернутьРезультат Тогда
		Закрыть(СкопироватьРезультатВНовоеХранилище());
	Иначе
		Закрыть(Истина);
	КонецЕсли; 
КонецПроцедуры	

&НаСервере
Функция СкопироватьРезультатВНовоеХранилище ()
	Если ЭтоАдресВременногоХранилища(АдресРезультатовЗадания) Тогда
		Возврат ПоместитьВоВременноеХранилище(ПолучитьИзВременногоХранилища(АдресРезультатовЗадания),Новый УникальныйИдентификатор);
	КонецЕсли;
	
	Возврат "";
КонецФункции	

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(ИДСинхронизации) Тогда
		Отказ = Истина;
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКонфигурациюЗавершение () Экспорт
	Попытка
		ДД  = ПолучитьИзВременногоХранилища(АдресРезультатовЗадания);
	    Путь = КаталогВременныхФайлов() + "update.apk";
		Если ДД = Неопределено Тогда
			ОбработкаОшибкиКлиент("Файл обновлений не загружен!");
			Возврат;
		КонецЕсли;	
		ДД.Записать(Путь);
		ЗапуститьПриложение(Путь);
	Исключение
		ОбработкаОшибкиКлиент(ОписаниеОшибки());
 	КонецПопытки;
КонецПроцедуры	