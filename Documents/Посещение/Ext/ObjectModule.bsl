
Процедура ОбработкаПроведения(Отказ, Режим)
	Движения.СостоянияПосещений.Записывать = Истина;
	
	Движение = Движения.СостоянияПосещений.Добавить();
	Движение.Период    = Дата;
	Движение.Сотрудник = Сотрудник;
	Движение.Посещение = Ссылка;
	Движение.Начато    = Истина;
	Движение.Завершено = Завершено;
	
	Если ВидПосещения = Перечисления.ВидыПосещений.ТелефонныйЗвонок Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	1 КАК Цифра,
		|	""1"" КАК Строка
		|ПОМЕСТИТЬ Цифры
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	2,
		|	""2""
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	3,
		|	""3""
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	4,
		|	""4""
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	5,
		|	""5""
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	6,
		|	""6""
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	7,
		|	""7""
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	8,
		|	""8""
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	9,
		|	""9""
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	0,
		|	""0""
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Цифра
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Сотни.Цифра * 100 + Десятки.Цифра * 10 + Цифры.Цифра КАК Число,
		|	Сотни.Строка + Десятки.Строка + Цифры.Строка КАК Строка
		|ПОМЕСТИТЬ Числа
		|ИЗ
		|	Цифры КАК Сотни
		|		ПОЛНОЕ СОЕДИНЕНИЕ Цифры КАК Десятки
		|		ПО (ИСТИНА)
		|		ПОЛНОЕ СОЕДИНЕНИЕ Цифры КАК Цифры
		|		ПО (ИСТИНА)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Число
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	&ДокументПосещение КАК Посещение,
		|	&Контрагент КАК Контрагент,
		|	Числа.Строка КАК ИдентификаторЗадания,
		|	&ДокументМаршрут КАК Маршрут,
		|	ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю.ВидЗадания КАК ВидЗадания,
		|	ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю.ВидЗадания.Наименование КАК Задание,
		|
		|	ВЫБОР
		|		КОГДА (ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю.ВидЗадания = &ВидЗаданияАктуализацияКонтактныхДанных)
		|			ТОГДА &ШаблонАнкетыАктуализацияКонтактныхДанных
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ШаблонАнкетыДляТП.ПустаяСсылка)
		|	КОНЕЦ КАК ШаблонАнкеты,
		|	ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю.Обязательное КАК Обязательное
		|ИЗ
		|	Справочник.ТипыПосещенийКлиентовТорговымиПредставителями.ЗаданияТорговомуПредставителю КАК ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю
		|		ЛЕВОЕ СОЕДИНЕНИЕ Числа КАК Числа
		|		ПО ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю.НомерСтроки = Числа.Число
		|ГДЕ
		|	ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ Числа
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ Цифры";
		
		Запрос.УстановитьПараметр("Ссылка", ОбщийМодульПовтИсп.ПолучитьТипПосещенияТелефонныйЗвонок() );
		Запрос.УстановитьПараметр("Контрагент", Контрагент);
		Запрос.УстановитьПараметр("ДокументПосещение", Ссылка);
		Запрос.УстановитьПараметр("ДокументМаршрут", Маршрут);
		
		Запрос.УстановитьПараметр("ВидЗаданияАктуализацияКонтактныхДанных", ОбщийМодульПовтИсп.ВидыЗаданийТорговымПредставителямПолучитьПредопределенныйЭлемент("АктуализацияКонтактныхДанных"));
		Запрос.УстановитьПараметр("ШаблонАнкетыАктуализацияКонтактныхДанных", ОбщийМодульПовтИсп.ШаблонАнкетыДляТППолучитьПредопределенныйЭлемент("АктуализацияКонтактныхДанных"));
		
		Движения.ЗаданияТППоВнеплановымПосещениям.Загрузить(Запрос.Выполнить().Выгрузить());
		Движения.ЗаданияТППоВнеплановымПосещениям.Записывать = Истина;
		
	ИначеЕсли ВидПосещения = Перечисления.ВидыПосещений.ВнеплановоеПосещение Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	1 КАК Цифра,
		|	""1"" КАК Строка
		|ПОМЕСТИТЬ Цифры
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	2,
		|	""2""
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	3,
		|	""3""
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	4,
		|	""4""
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	5,
		|	""5""
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	6,
		|	""6""
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	7,
		|	""7""
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	8,
		|	""8""
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	9,
		|	""9""
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	0,
		|	""0""
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Цифра
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Сотни.Цифра * 100 + Десятки.Цифра * 10 + Цифры.Цифра КАК Число,
		|	Сотни.Строка + Десятки.Строка + Цифры.Строка КАК Строка
		|ПОМЕСТИТЬ Числа
		|ИЗ
		|	Цифры КАК Сотни
		|		ПОЛНОЕ СОЕДИНЕНИЕ Цифры КАК Десятки
		|		ПО (ИСТИНА)
		|		ПОЛНОЕ СОЕДИНЕНИЕ Цифры КАК Цифры
		|		ПО (ИСТИНА)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Число
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	&ДокументПосещение КАК Посещение,
		|	&Контрагент КАК Контрагент,
		|	Числа.Строка КАК ИдентификаторЗадания,
		|	&ДокументМаршрут КАК Маршрут,
		|	ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю.ВидЗадания КАК ВидЗадания,
		|	ВЫБОР
		|		КОГДА ЗаданияПоАнкетированиюКлиентов.ШаблонАнкеты ЕСТЬ NULL
		|			ТОГДА ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю.ВидЗадания.Наименование
		|		ИНАЧЕ ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю.ВидЗадания.Наименование + "" ("" + ЗаданияПоАнкетированиюКлиентов.ШаблонАнкеты.Наименование + "")""
		|	КОНЕЦ КАК Задание,
		|	ВЫБОР
		|		КОГДА НЕ ЗаданияНаАктуализациюКонтактныхДанных.ШаблонАнкеты ЕСТЬ NULL
		|			ТОГДА ЗаданияНаАктуализациюКонтактныхДанных.ШаблонАнкеты
		|		КОГДА ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю.ВидЗадания = &ВидЗаданияАктуализацияКонтактныхДанных
		|			ТОГДА &ШаблонАнкетыАктуализацияКонтактныхДанных
		|		КОГДА НЕ ЗаданияПоАнкетированиюКлиентов.ШаблонАнкеты ЕСТЬ NULL
		|			ТОГДА ЗаданияПоАнкетированиюКлиентов.ШаблонАнкеты
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ШаблонАнкетыДляТП.ПустаяСсылка)
		|	КОНЕЦ КАК ШаблонАнкеты,
		|	ВЫБОР
		|		КОГДА НЕ ЗаданияНаАктуализациюКонтактныхДанных.ШаблонАнкеты ЕСТЬ NULL
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю.Обязательное
		|	КОНЕЦ КАК Обязательное
		|ИЗ
		|	Справочник.ТипыПосещенийКлиентовТорговымиПредставителями.ЗаданияТорговомуПредставителю КАК ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю
		|		ЛЕВОЕ СОЕДИНЕНИЕ Числа КАК Числа
		|		ПО ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю.НомерСтроки = Числа.Число
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияПоАнкетированиюКлиентов КАК ЗаданияПоАнкетированиюКлиентов
		|		ПО (ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю.ВидЗадания = &ВидЗаданияАнкета)
		|			И (ЗаданияПоАнкетированиюКлиентов.Контрагент = &Контрагент)
		|			И (ЗаданияПоАнкетированиюКлиентов.ТорговыйПредставитель = &ТорговыйПредставитель)
		|			И (ЗаданияПоАнкетированиюКлиентов.ДатаНачалаАнкетирования <= &Дата)
		|			И (ЗаданияПоАнкетированиюКлиентов.ШаблонАнкеты <> &ШаблонАнкетыАктуализацияКонтактныхДанных)
		|			И (&Дата <= КОНЕЦПЕРИОДА(ЗаданияПоАнкетированиюКлиентов.ДатаОкончанияАнкетирования, ДЕНЬ)
		|				ИЛИ ЗаданияПоАнкетированиюКлиентов.ДатаОкончанияАнкетирования = ДАТАВРЕМЯ(1, 1, 1))
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АнкетаТП КАК АнкетаТП
		|		ПО (НЕ ЗаданияПоАнкетированиюКлиентов.Контрагент ЕСТЬ NULL)
		|			И (АнкетаТП.Дата МЕЖДУ НАЧАЛОПЕРИОДА(ЗаданияПоАнкетированиюКлиентов.ДатаНачалаАнкетирования, ДЕНЬ) И ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ), СЕКУНДА, -1))
		|			И (НЕ АнкетаТП.ПометкаУдаления)
		|			И (АнкетаТП.Контрагент = &Контрагент)
		|			И (ЗаданияПоАнкетированиюКлиентов.ШаблонАнкеты = АнкетаТП.ШаблонАнкеты)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияПоАнкетированиюКлиентов КАК ЗаданияНаАктуализациюКонтактныхДанных
		|		ПО (ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю.ВидЗадания = &ВидЗаданияАктуализацияКонтактныхДанных)
		|			И (ЗаданияНаАктуализациюКонтактныхДанных.Контрагент = &Контрагент)
		|			И (ЗаданияНаАктуализациюКонтактныхДанных.ТорговыйПредставитель = &ТорговыйПредставитель)
		|			И (ЗаданияНаАктуализациюКонтактныхДанных.ДатаНачалаАнкетирования <= &Дата)
		|			И (ЗаданияНаАктуализациюКонтактныхДанных.ШаблонАнкеты = &ШаблонАнкетыАктуализацияКонтактныхДанных)
		|			И (&Дата <= ЗаданияНаАктуализациюКонтактныхДанных.ДатаОкончанияАнкетирования
		|				ИЛИ ЗаданияНаАктуализациюКонтактныхДанных.ДатаОкончанияАнкетирования = ДАТАВРЕМЯ(1, 1, 1))
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АнкетаТП КАК АнкетаТПНаАктуализациюКонтактныхДанных
		|		ПО (НЕ ЗаданияНаАктуализациюКонтактныхДанных.Контрагент ЕСТЬ NULL)
		|			И (АнкетаТПНаАктуализациюКонтактныхДанных.Дата МЕЖДУ НАЧАЛОПЕРИОДА(ЗаданияНаАктуализациюКонтактныхДанных.ДатаНачалаАнкетирования, ДЕНЬ) И ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ), СЕКУНДА, -1))
		|			И (НЕ АнкетаТПНаАктуализациюКонтактныхДанных.ПометкаУдаления)
		|			И (АнкетаТПНаАктуализациюКонтактныхДанных.Контрагент = &Контрагент)
		|			И (ЗаданияНаАктуализациюКонтактныхДанных.ШаблонАнкеты = АнкетаТПНаАктуализациюКонтактныхДанных.ШаблонАнкеты)
		|ГДЕ
		|	ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю.Ссылка = &Ссылка
		|	И (ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю.ВидЗадания <> &ВидЗаданияАнкета
		|				И ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю.ВидЗадания <> &ВидЗаданияАктуализацияКонтактныхДанных
		|			ИЛИ ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю.ВидЗадания = &ВидЗаданияАнкета
		|				И НЕ ЗаданияПоАнкетированиюКлиентов.Контрагент ЕСТЬ NULL
		|				И АнкетаТП.Дата ЕСТЬ NULL
		|			ИЛИ ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю.ВидЗадания = &ВидЗаданияАктуализацияКонтактныхДанных
		|				И НЕ ЗаданияНаАктуализациюКонтактныхДанных.Контрагент ЕСТЬ NULL
		|				И АнкетаТПНаАктуализациюКонтактныхДанных.Дата ЕСТЬ NULL)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ Числа
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ Цифры";
		
		Запрос.УстановитьПараметр("Ссылка", ОбщийМодульПовтИсп.ПолучитьТипВнеплановогоПосещения() );
		Запрос.УстановитьПараметр("Контрагент", Контрагент);
		Запрос.УстановитьПараметр("ДокументПосещение", Ссылка);
		Запрос.УстановитьПараметр("ДокументМаршрут", Маршрут);
		
		Запрос.УстановитьПараметр("ТорговыйПредставитель", Сотрудник.ФизЛицо);
		Запрос.УстановитьПараметр("Дата", Дата);
		Запрос.УстановитьПараметр("ВидЗаданияАнкета", ОбщийМодульПовтИсп.ВидыЗаданийТорговымПредставителямПолучитьПредопределенныйЭлемент("Анкета"));
		Запрос.УстановитьПараметр("ВидЗаданияАктуализацияКонтактныхДанных", ОбщийМодульПовтИсп.ВидыЗаданийТорговымПредставителямПолучитьПредопределенныйЭлемент("АктуализацияКонтактныхДанных"));
		Запрос.УстановитьПараметр("ШаблонАнкетыАктуализацияКонтактныхДанных", ОбщийМодульПовтИсп.ШаблонАнкетыДляТППолучитьПредопределенныйЭлемент("АктуализацияКонтактныхДанных"));
		
		Движения.ЗаданияТППоВнеплановымПосещениям.Загрузить(Запрос.Выполнить().Выгрузить());
		Движения.ЗаданияТППоВнеплановымПосещениям.Записывать = Истина;
		
	КонецЕсли;
	
	// Перепроведем маршруты, чтобы убрать из них задание на анкетирование
	Если Не Отказ Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	АнкетаТП.ТорговыйПредставитель,
		|	АнкетаТП.Ссылка
		|ПОМЕСТИТЬ ДокументыАнкетирования
		|ИЗ
		|	Документ.АнкетаТП КАК АнкетаТП
		|ГДЕ
		|	АнкетаТП.Посещение = &Посещение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Маршрут.Ссылка КАК Маршрут
		|ИЗ
		|	Документ.Маршрут КАК Маршрут
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыАнкетирования КАК ДокументыАнкетирования
		|		ПО Маршрут.ТорговыйПредставитель = ДокументыАнкетирования.ТорговыйПредставитель
		|ГДЕ
		|	Маршрут.Дата > &Дата
		|	И Маршрут.Проведен
		|
		|УПОРЯДОЧИТЬ ПО
		|	Маршрут
		|АВТОУПОРЯДОЧИВАНИЕ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДокументыАнкетирования";
		
		Запрос.УстановитьПараметр("Дата", КонецДня(ЭтотОбъект.Дата));
		Запрос.УстановитьПараметр("Посещение", ЭтотОбъект.Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			укМаршрут = ВыборкаДетальныеЗаписи.Маршрут.ПолучитьОбъект();
			укМаршрут.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если Не ЗначениеЗаполнено(Сотрудник) Тогда
		Сотрудник = Константы.ТекущийПользователь.Получить();
	КонецЕсли;	
	ТорговыйПредставитель = Сотрудник.ФизЛицо;
	
КонецПроцедуры
