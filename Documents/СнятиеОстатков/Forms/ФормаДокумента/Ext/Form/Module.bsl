
&НаКлиенте
Процедура ТоварыПослеУдаления(Элемент)
	ОбновитьНадписьТовары();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	ОбновитьНадписьТовары();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ДобавленыТовары" И Источник.ВладелецФормы = ЭтаФорма Тогда
		ЗагрузитьТоварыИзХранилища(Параметр);
		Модифицированность = Истина;
		Этаформа.ТолькоПросмотр = Ложь; //В МП почему то устанавливается Истина
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьТовар()	
	Парам = Новый Структура("АдресХранилищаТоваров,Контрагент,ДатаДоставки,Посещение,ТекДокумент", ПолучитьАдресХранилищаТоваров(), Объект.Контрагент, ТекущаяДата(), Объект.Посещение, Объект.Ссылка);
	Парам.Вставить("ЭтоСнятиеОстатков", Истина);
	
	НоваяФорма = ОткрытьФорму("Справочник.Номенклатура.ФормаВыбора", Парам, ЭтаФорма);
	Попытка
		НоваяФорма.Элементы.КомандаСохранить.Заголовок = "Сохранить снятие остатков"
	Исключение
	КонецПопытки;
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	
	ДобавитьТовар();
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ОбщийМодульКлиент.ЗафиксироватьМестоположение(Объект.Ссылка, "Снятие остатков");
КонецПроцедуры

&НаКлиенте
Процедура ПриВводеОстаткаНоменклатуры(Результат, ДопПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		Элементы.Товары.ТекущиеДанные.Количество = Результат;
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТоварыВыборЗавершение (Рез, ДополнительныеПараметры) Экспорт
	Если Рез = Неопределено Тогда
	ИначеЕсли Рез.КоличествоЗаказано = 0 Тогда
		Объект.Товары.Удалить( ДополнительныеПараметры.ТекСтрока.НомерСтроки-1 );
		Модифицированность = Истина;
	Иначе
		ТекСтрока = ДополнительныеПараметры.ТекСтрока;
		ЗаполнитьЗначенияСвойств(ТекСтрока, Рез);
		ТекСтрока.Количество = Рез.КоличествоЗаказано;
		Модифицированность = Истина;
	КонецЕсли; 
КонецПроцедуры	

&НаКлиенте
Процедура ТоварыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	//Всп = Элементы.Товары.ТекущиеДанные.Количество;
	//ПоказатьВводЧисла( Новый ОписаниеОповещения( "ПриВводеОстаткаНоменклатуры", ЭтотОбъект ), Всп, "Введите количество остатка", 15, 3);
	
	ТекСтрока = Элементы.Товары.ТекущиеДанные;
	
	Парам = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры,Цена,ЕдиницаИзмерения");
	ЗаполнитьЗначенияСвойств(Парам, ТекСтрока);
	Парам.Вставить("Контрагент", ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка"));
	Парам.Вставить("КоличествоЗаказано", ТекСтрока.Количество);
	Парам.Вставить("ЭтоВозврат"        , Истина);
	
	ДополнительныеПараметры = Новый Структура("ТекСтрока", ТекСтрока);
	ОписаниеОповещения   = Новый ОписаниеОповещения("ТоварыВыборЗавершение", ЭтаФорма, ДополнительныеПараметры);
	ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаВводаПараметровНоменклатуры", Парам, , , , , ОписаниеОповещения);
	
КонецПроцедуры

// СЕРВЕРНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ //

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	// 2017-05-02 МСН
	Если Параметры.Свойство("Ссылка") И ТипЗнч(Параметры.Ссылка) = Тип("ДокументСсылка.СнятиеОстатков") Тогда
		ЗначениеВРеквизитФормы(Параметры.Ссылка.ПолучитьОбъект(), "Объект");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.Ответственный = Константы.ТекущийПользователь.Получить();
	КонецЕсли;	
	ОбновитьНадписьТовары();
КонецПроцедуры

&НаСервере
Процедура ОбновитьНадписьТовары ()
	Элементы.НадписьДобавьте.Видимость = Объект.Товары.Количество() = 0;
КонецПроцедуры	

&НаСервере
Процедура ЗагрузитьТоварыИзХранилища(Параметр)
	Если ТипЗнч(Параметр) = Тип("Массив") Тогда
		Объект.Товары.Очистить();
		Для каждого структСтроки Из Параметр Цикл
			Если ТипЗнч(структСтроки) = Тип("Структура") Тогда
				ЗаполнитьЗначенияСвойств(Объект.Товары.Добавить(), структСтроки);
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли ЭтоАдресВременногоХранилища(Параметр) Тогда
		дТЗ = ПолучитьИзВременногоХранилища(Параметр);
		Объект.Товары.Загрузить(дТЗ);
	КонецЕсли;	
	ОбновитьНадписьТовары();
КонецПроцедуры	

&НаСервере
Функция ПолучитьАдресХранилищаТоваров ()
	Рез = Объект.Товары.Выгрузить();
	Возврат ПоместитьВоВременноеХранилище(Рез,ЭтаФорма.УникальныйИдентификатор);
КонецФункции	

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПодключитьОбработчикОжидания("ДобавитьТовар", 0.1, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Если Не ЗначениеЗаполнено(Объект.Ссылка) И Объект.Товары.Количество() = 0 Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;	
КонецПроцедуры
