Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	Набор = Движения.АктуальныеМаршруты;
	Набор.Очистить();
	Набор.Записывать = Истина;
	
	Запись                       = Набор.Добавить();
	Запись.Маршрут               = Ссылка;
	Запись.ТорговыйПредставитель = ТорговыйПредставитель;
	Запись.Период                = Дата;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МаршрутПланПосещений.Контрагент КАК Контрагент,
	|	МаршрутЗадания.ИдентификаторЗадания,
	|	МаршрутЗадания.Ссылка КАК Маршрут,
	|	ВЫБОР
	|		КОГДА МаршрутЗадания.Задание = ""Заказ""
	|			ТОГДА &ВидЗаданияЗаказ
	|		КОГДА МаршрутЗадания.Задание = ""Фото фасада торговой точки""
	|			ТОГДА &ВидЗаданияФотоФасада
	|		КОГДА МаршрутЗадания.Задание = ""Фото полки до мерчандайзинга""
	|			ТОГДА &ВидЗаданияФотоПолкиДоМерчендайзинга
	|		КОГДА МаршрутЗадания.Задание = ""Фото полки после мерчандайзинга""
	|			ТОГДА &ВидЗаданияФотоПолкиПослеМерчендайзинга
	|		КОГДА МаршрутЗадания.Задание = ""Покупка""
	|			ТОГДА &ВидЗаданияВозврат
	|		КОГДА МаршрутЗадания.Задание = ""Комментарий""
	|			ТОГДА &ВидЗаданияКомментарий
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ВидыЗаданийТорговымПредставителям.ПустаяСсылка)
	|	КОНЕЦ КАК ВидЗадания,
	|	ВЫБОР
	|		КОГДА МаршрутЗадания.Задание = ""Покупка""
	|			ТОГДА ""Возврат""
	|		ИНАЧЕ МаршрутЗадания.Задание
	|	КОНЕЦ КАК Задание,
	|	МаршрутЗадания.ШаблонАнкеты,
	|	ЛОЖЬ КАК Обязательное,
	|	0 КАК НомерПосещения,
	|	МаршрутПланПосещений.НомерСтроки КАК НомерСтроки,
	|	0 КАК ПорНомерЗадания
	|ИЗ
	|	Документ.Маршрут.Задания КАК МаршрутЗадания
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Маршрут.ПланПосещений КАК МаршрутПланПосещений
	|		ПО МаршрутЗадания.Ссылка = МаршрутПланПосещений.Ссылка
	|			И МаршрутЗадания.ИдентификаторПлана = МаршрутПланПосещений.Идентификатор
	|ГДЕ
	|	МаршрутЗадания.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	МаршрутЗаданияТП.Контрагент,
	|	ВидыЗаданийТорговымПредставителям.Код,
	|	МаршрутЗаданияТП.Ссылка,
	|	ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю.ВидЗадания,
	|	ВидыЗаданийТорговымПредставителям.Наименование + ВЫБОР
	|		КОГДА НЕ ЗаданияПоАнкетированиюКлиентов.ШаблонАнкеты ЕСТЬ NULL
	|			ТОГДА "" ("" + ЗаданияПоАнкетированиюКлиентов.ШаблонАнкеты.Наименование + "")""
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ ЗаданияНаАктуализациюКонтактныхДанных.ШаблонАнкеты ЕСТЬ NULL
	|			ТОГДА ЗаданияНаАктуализациюКонтактныхДанных.ШаблонАнкеты
	|		КОГДА ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю.ВидЗадания = &ВидЗаданияАктуализацияКонтактныхДанных
	|			ТОГДА &ШаблонАнкетыАктуализацияКонтактныхДанных
	|		КОГДА НЕ ЗаданияПоАнкетированиюКлиентов.ШаблонАнкеты ЕСТЬ NULL
	|			ТОГДА ЗаданияПоАнкетированиюКлиентов.ШаблонАнкеты
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ШаблонАнкетыДляТП.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ ЗаданияНаАктуализациюКонтактныхДанных.ШаблонАнкеты ЕСТЬ NULL
	|			ТОГДА АнкетаТПНаАктуализациюКонтактныхДанных.Дата ЕСТЬ NULL
	|		ИНАЧЕ ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю.Обязательное
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА МаршрутЗаданияТП.НомерПосещения > 0
	|			ТОГДА МаршрутЗаданияТП.НомерПосещения
	|		ИНАЧЕ 999
	|	КОНЕЦ,
	|	ВидыЗаданийТорговымПредставителям.Код,
	|	ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю.НомерСтроки
	|ИЗ
	|	Документ.Маршрут.ЗаданияТП КАК МаршрутЗаданияТП
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТипыПосещенийКлиентовТорговымиПредставителями.ЗаданияТорговомуПредставителю КАК ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗаданийТорговымПредставителям КАК ВидыЗаданийТорговымПредставителям
	|			ПО ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю.ВидЗадания = ВидыЗаданийТорговымПредставителям.Ссылка
	|		ПО МаршрутЗаданияТП.ТипПосещения = ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияПоАнкетированиюКлиентов КАК ЗаданияПоАнкетированиюКлиентов
	|		ПО (ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю.ВидЗадания = &ВидЗаданияАнкета)
	|			И МаршрутЗаданияТП.Контрагент = ЗаданияПоАнкетированиюКлиентов.Контрагент
	|			И МаршрутЗаданияТП.Ссылка.ТорговыйПредставитель = ЗаданияПоАнкетированиюКлиентов.ТорговыйПредставитель
	|			И МаршрутЗаданияТП.Ссылка.Дата >= ЗаданияПоАнкетированиюКлиентов.ДатаНачалаАнкетирования
	|			И (ЗаданияПоАнкетированиюКлиентов.ШаблонАнкеты <> &ШаблонАнкетыАктуализацияКонтактныхДанных)
	|			И (МаршрутЗаданияТП.Ссылка.Дата <= КОНЕЦПЕРИОДА(ЗаданияПоАнкетированиюКлиентов.ДатаОкончанияАнкетирования, ДЕНЬ)
	|				ИЛИ ЗаданияПоАнкетированиюКлиентов.ДатаОкончанияАнкетирования = ДАТАВРЕМЯ(1, 1, 1))
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АнкетаТП КАК АнкетаТП
	|		ПО (НЕ ЗаданияПоАнкетированиюКлиентов.Контрагент ЕСТЬ NULL)
	|			И (АнкетаТП.Дата МЕЖДУ НАЧАЛОПЕРИОДА(ЗаданияПоАнкетированиюКлиентов.ДатаНачалаАнкетирования, ДЕНЬ) И ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(МаршрутЗаданияТП.Ссылка.Дата, ДЕНЬ), СЕКУНДА, -1))
	|			И (НЕ АнкетаТП.ПометкаУдаления)
	|			И МаршрутЗаданияТП.Контрагент = АнкетаТП.Контрагент
	|			И (ЗаданияПоАнкетированиюКлиентов.ШаблонАнкеты = АнкетаТП.ШаблонАнкеты)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияПоАнкетированиюКлиентов КАК ЗаданияНаАктуализациюКонтактныхДанных
	|		ПО (ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю.ВидЗадания = &ВидЗаданияАктуализацияКонтактныхДанных)
	|			И МаршрутЗаданияТП.Контрагент = ЗаданияНаАктуализациюКонтактныхДанных.Контрагент
	|			И МаршрутЗаданияТП.Ссылка.ТорговыйПредставитель = ЗаданияНаАктуализациюКонтактныхДанных.ТорговыйПредставитель
	|			И МаршрутЗаданияТП.Ссылка.Дата >= ЗаданияНаАктуализациюКонтактныхДанных.ДатаНачалаАнкетирования
	|			И (ЗаданияНаАктуализациюКонтактныхДанных.ШаблонАнкеты = &ШаблонАнкетыАктуализацияКонтактныхДанных)
	|			И (МаршрутЗаданияТП.Ссылка.Дата <= ЗаданияНаАктуализациюКонтактныхДанных.ДатаОкончанияАнкетирования
	|				ИЛИ ЗаданияНаАктуализациюКонтактныхДанных.ДатаОкончанияАнкетирования = ДАТАВРЕМЯ(1, 1, 1))
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.АнкетаТП КАК АнкетаТПНаАктуализациюКонтактныхДанных
	|		ПО (НЕ ЗаданияНаАктуализациюКонтактныхДанных.Контрагент ЕСТЬ NULL)
	|			И (АнкетаТПНаАктуализациюКонтактныхДанных.Дата МЕЖДУ НАЧАЛОПЕРИОДА(ЗаданияНаАктуализациюКонтактныхДанных.ДатаНачалаАнкетирования, ДЕНЬ) И КОНЕЦПЕРИОДА(МаршрутЗаданияТП.Ссылка.Дата, ДЕНЬ))
	|			И (НЕ АнкетаТПНаАктуализациюКонтактныхДанных.ПометкаУдаления)
	|			И МаршрутЗаданияТП.Контрагент = АнкетаТПНаАктуализациюКонтактныхДанных.Контрагент
	|			И (ЗаданияНаАктуализациюКонтактныхДанных.ШаблонАнкеты = АнкетаТПНаАктуализациюКонтактныхДанных.ШаблонАнкеты)
	|ГДЕ
	|	МаршрутЗаданияТП.Ссылка = &Ссылка
	|	И (ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю.ВидЗадания <> &ВидЗаданияАнкета
	|				И ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю.ВидЗадания <> &ВидЗаданияАктуализацияКонтактныхДанных
	|			ИЛИ ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю.ВидЗадания = &ВидЗаданияАнкета
	|				И НЕ ЗаданияПоАнкетированиюКлиентов.Контрагент ЕСТЬ NULL
	|				И АнкетаТП.Дата ЕСТЬ NULL
	|			ИЛИ ТипыПосещенийКлиентовТорговымиПредставителямиЗаданияТорговомуПредставителю.ВидЗадания = &ВидЗаданияАктуализацияКонтактныхДанных
	|				И НЕ ЗаданияНаАктуализациюКонтактныхДанных.Контрагент ЕСТЬ NULL
	|				И АнкетаТПНаАктуализациюКонтактныхДанных.Дата ЕСТЬ NULL)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Маршрут,
	|	Контрагент,
	|	ПорНомерЗадания,
	|	НомерСтроки
	|АВТОУПОРЯДОЧИВАНИЕ";
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Запрос.УстановитьПараметр("ВидЗаданияЗаказ", ОбщийМодульПовтИсп.ВидыЗаданийТорговымПредставителямПолучитьПредопределенныйЭлемент("Заказ"));
	Запрос.УстановитьПараметр("ВидЗаданияФотоФасада", ОбщийМодульПовтИсп.ВидыЗаданийТорговымПредставителямПолучитьПредопределенныйЭлемент("ФотоФасада"));
	Запрос.УстановитьПараметр("ВидЗаданияФотоПолкиДоМерчендайзинга", ОбщийМодульПовтИсп.ВидыЗаданийТорговымПредставителямПолучитьПредопределенныйЭлемент("ФотоПолкиДоМерчендайзинга"));
	Запрос.УстановитьПараметр("ВидЗаданияФотоПолкиПослеМерчендайзинга", ОбщийМодульПовтИсп.ВидыЗаданийТорговымПредставителямПолучитьПредопределенныйЭлемент("ФотоПолкиПослеМерчендайзинга"));
	Запрос.УстановитьПараметр("ВидЗаданияВозврат", ОбщийМодульПовтИсп.ВидыЗаданийТорговымПредставителямПолучитьПредопределенныйЭлемент("Возврат"));
	Запрос.УстановитьПараметр("ВидЗаданияКомментарий", ОбщийМодульПовтИсп.ВидыЗаданийТорговымПредставителямПолучитьПредопределенныйЭлемент("Комментарий"));
	
	Запрос.УстановитьПараметр("ВидЗаданияАнкета", ОбщийМодульПовтИсп.ВидыЗаданийТорговымПредставителямПолучитьПредопределенныйЭлемент("Анкета"));
	Запрос.УстановитьПараметр("ВидЗаданияАктуализацияКонтактныхДанных", ОбщийМодульПовтИсп.ВидыЗаданийТорговымПредставителямПолучитьПредопределенныйЭлемент("АктуализацияКонтактныхДанных"));
	Запрос.УстановитьПараметр("ШаблонАнкетыАктуализацияКонтактныхДанных", ОбщийМодульПовтИсп.ШаблонАнкетыДляТППолучитьПредопределенныйЭлемент("АктуализацияКонтактныхДанных"));
	
	Движения.ЗаданияТП.Загрузить(Запрос.Выполнить().Выгрузить());
	Движения.ЗаданияТП.Записывать = Истина;
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	Набор = Движения.АктуальныеМаршруты;
	Набор.Очистить();
	Набор.Записывать = Истина;
	
	Набор = Движения.ЗаданияТП;
	Набор.Очистить();
	Набор.Записывать = Истина;
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	Если Не Отказ И Проведен Тогда
		Набор = РегистрыСведений.АктуальныеМаршруты.СоздатьНаборЗаписей();
		Набор.Отбор.Регистратор.Значение = Ссылка;
		Набор.Отбор.Регистратор.Использование = Истина;
		Набор.Отбор.Регистратор.ВидСравнения = ВидСравнения.Равно;
		Набор.Записать( Истина );
		
		Набор = РегистрыСведений.ЗаданияТП.СоздатьНаборЗаписей();
		Набор.Отбор.Регистратор.Значение = Ссылка;
		Набор.Отбор.Регистратор.Использование = Истина;
		Набор.Отбор.Регистратор.ВидСравнения = ВидСравнения.Равно;
		Набор.Записать( Истина );
	КонецЕсли;
КонецПроцедуры
