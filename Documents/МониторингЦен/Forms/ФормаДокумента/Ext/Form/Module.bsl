
&НаКлиенте
Процедура ТоварыПослеУдаления(Элемент)
	ОбновитьНадписьТовары();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	ОбновитьНадписьТовары();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ДобавленыТовары" И Источник.ВладелецФормы = ЭтаФорма Тогда
		ЗагрузитьТоварыИзХранилища(Параметр);
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	Парам = Новый Структура("ТипЦен,АдресХранилищаТоваров,Контрагент,Посещение",Объект.ТипЦен,ПолучитьАдресХранилищаТоваров(),Объект.Контрагент,Объект.Посещение);
	ОткрытьФорму("Документ.МониторингЦен.Форма.ФормаВыбораНоменклатуры",Парам,ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ОбщийМодульКлиент.ЗафиксироватьМестоположение(Объект.Ссылка, "Мониторинг цен");
КонецПроцедуры

&НаКлиенте
Процедура ПриВводеЦеныНоменклатуры(Результат, ДопПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		ТекСтрока = Элементы.Товары.ТекущиеДанные;
		
		ЗаполнитьЗначенияСвойств(ТекСтрока, Результат, "Цена");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТоварыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекСтрока = Элементы.Товары.ТекущиеДанные;
	
	Парам = Новый Структура("Номенклатура,ХарактеристикаНоменклатуры,Цена,ЕдиницаИзмерения");
	ЗаполнитьЗначенияСвойств(Парам, ТекСтрока);
	Парам.Вставить("Контрагент", Объект.Контрагент);
	
	СтандартнаяОбработка = Ложь;
	
	// 2017-04-27 МСН
	Форма = ОткрытьФорму( "Справочник.Номенклатура.Форма.ФормаВводаЦены", Парам, ЭтаФорма, , , , Новый ОписаниеОповещения( "ПриВводеЦеныНоменклатуры", ЭтотОбъект ), );
	Форма.ЗакрыватьПриВыборе = Истина;
	Форма.ЗакрыватьПриЗакрытииВладельца = Истина;
	
	//Рез = ОткрытьФормуМодально("Справочник.Номенклатура.Форма.ФормаВводаЦены",Парам);
	//Если Рез <> Неопределено Тогда
	//	ЗаполнитьЗначенияСвойств(ТекСтрока,Рез,"Цена");
	//КонецЕсли; 
КонецПроцедуры

// СЕРВЕРНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ //

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.Ответственный = Константы.ТекущийПользователь.Получить();
		Объект.ТипЦен        = ДополнительныеФункцииСервер.ПолучитьАктуальныйДоговорКонтрагента(Объект.Контрагент).ТипЦен;
	КонецЕсли;	
	ОбновитьНадписьТовары();
КонецПроцедуры

&НаСервере
Процедура ОбновитьНадписьТовары ()
	Элементы.НадписьДобавьте.Видимость = Объект.Товары.Количество() = 0;
КонецПроцедуры	

&НаСервере
Процедура ЗагрузитьТоварыИзХранилища (Параметр)
	Если ТипЗнч(Параметр) = Тип("Массив") Тогда
		Объект.Товары.Очистить();
		Для каждого структСтроки Из Параметр Цикл
			Если ТипЗнч(структСтроки) = Тип("Структура") Тогда
				ЗаполнитьЗначенияСвойств(Объект.Товары.Добавить(), структСтроки);
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли ЭтоАдресВременногоХранилища(Параметр) Тогда
		Объект.Товары.Загрузить(ПолучитьИзВременногоХранилища(Параметр));
	КонецЕсли;	
	ОбновитьНадписьТовары();
КонецПроцедуры	

&НаСервере
Функция ПолучитьАдресХранилищаТоваров ()
	Рез = Объект.Товары.Выгрузить();
	Возврат ПоместитьВоВременноеХранилище(Рез,ЭтаФорма.УникальныйИдентификатор);
КонецФункции	
